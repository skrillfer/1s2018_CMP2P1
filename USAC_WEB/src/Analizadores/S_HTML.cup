package Analizadores; 

import Estructuras.*;
import java_cup.runtime.*;

import java.util.*;


action code {:
    //se declaran variables globales etc.
    public int Index=1;
    public NodoDOM root;
    public NodoDOM tmpN ;
    public NodoDOM last ;
    public NodoDOM gCuerpo ;
    public Stack<NodoDOM> pilaPanel = new Stack<>() ; // se guarda el ultimo panel
:}

parser code {:
    public NodoDOM crearNodo(String nombre,String valor,int linea,int columna){
        NodoDOM nuevo = new NodoDOM(nombre,valor,linea,columna,action_obj.Index);
        action_obj.Index++;
        return nuevo;
    }

    //este metodo crear un nuevo nodo, lo agrega a la raiz actual y luego cambia la raizactual por el nuevo nodo creado
    public void m1(String nombre,String valor,int linea,int columna){
        NodoDOM nuevo = new NodoDOM(nombre,valor,linea,columna,action_obj.Index);
        action_obj.Index++;
        action_obj.tmpN.add(nuevo);
        action_obj.tmpN=nuevo;         //despues de crear el nuevo nodo se deja en la variable tmpN
    }


    //este metodo crear un nuevo nodo y simplemente se lo agrega a la raiz actual sin cambiarla
    public void m2(String nombre,String valor,int linea,int columna){
        NodoDOM nuevo = new NodoDOM(nombre,valor,linea,columna,action_obj.Index);
        action_obj.Index++;
        action_obj.tmpN.add(nuevo);
        action_obj.last=nuevo;

        if(nombre.equals("panel")){
            if(action_obj.pilaPanel.isEmpty()){
                action_obj.gCuerpo=action_obj.tmpN;
            }
            action_obj.tmpN=nuevo;
            push(nuevo); // se agrega a la pila de paneles
        }
    }

    //este metodo hace tmpN=last
    public void Tadd(NodoDOM nodo){
        action_obj.tmpN.add(nodo);
    }

    //este metodo agrega un panel al la cima de la pila
    public void push(NodoDOM panel){
        action_obj.pilaPanel.push(panel);
    }

    public void pop(){
        action_obj.pilaPanel.pop();
        if(action_obj.pilaPanel.isEmpty()){
            action_obj.tmpN=action_obj.gCuerpo;
        }else{
            action_obj.tmpN=action_obj.pilaPanel.pop();
            action_obj.pilaPanel.push(action_obj.tmpN);
        }
    }

    //Este metodo retorna la RAIZ del arbol  generado 
    public NodoDOM getRoot(){
        return action_obj.root;
    }
:}

terminal token MAS,MENOS,POR,DIV,NUM,DECIMAL,I_D,INT,DOUBLE,STRING,CHAR,BOOLEAN,PYC,
IGUAL,VOID,APAR,CPAR,ALLA,CLLA,IHTML,IENCABEZADO,ICUERPO,ITITULO,IPANEL,ITEXTO,IIMAGEN,
IBOTON,IENLACE,ITABLA,IFIL_T,ICB,ICT,ITEXTO_A,ICAJA_TEXTO,ICAJA,IOPCION,ISPINNER,ISALTOF,
FHTML,FENCABEZADO,FCUERPO,FTITULO,FPANEL,FTEXTO,FIMAGEN,FBOTON,FENLACE,FTABLA,FFIL_T,FCB,
FCT,FTEXTO_A,FCAJA_TEXTO,FCAJA,FOPCION,FSPINNER,MAYQ,MENQ,ICJS,FCJS,ICSS,FCSS,FONDO,RUTA,
CLICK,VALOR,ID,GRUPO,ALTO,ANCHO,ALINEADO,CCSS,STRING_LITERAL,IZQ,DER,CENT;

non terminal NodoDOM programa,raiz,contenidohtml,encabezado,contenidoencabezado,cuerpo,
contenidocuerpo,contenidocjs,contenidocss,etiquetas_encabezado,fin_encabezado,et_encabezado,
etiquetacss,etiquetacjs,etiquetas_cuerpo,et_cuerpo,etiquetatitulo,contenidopanel,atributo,
list_atts,list_atts2,att_ruta,att_click,att_valor,att_id,att_grupo,att_alto,att_ancho,att_alineado,
valor_alineado,contenidotabla,etiquetastabla,contenidofila,etiquetasfila,et_cb_ct,contenidoct,contenidocb,
et_fila,cont_celda, contenidos_celdas;


/*
precedence left MAS MENOS;
precedence left POR DIV;
*/

start with programa;

programa ::= raiz;

raiz ::= MENQ  IHTML:x MAYQ   MENQ {: root = parser.crearNodo("chtml","",x.getLinea(),x.getColumna());  tmpN=root; :} contenidohtml;

contenidohtml ::=  IENCABEZADO:x            MAYQ  {:parser.m2("encabezado","",x.getLinea(),x.getColumna());:}  contenidoencabezado
                   |ICUERPO:x   list_atts   MAYQ  {:parser.m1("cuerpo","",x.getLinea(),x.getColumna());  :}      contenidocuerpo
                   |FHTML MAYQ;

contenidoencabezado ::=  etiquetas_encabezado fin_encabezado
                        |MENQ FENCABEZADO MAYQ    MENQ FHTML MAYQ
                        |MENQ FENCABEZADO MAYQ    MENQ ICUERPO:x  {: parser.m1("cuerpo","",x.getLinea(),x.getColumna()); :} list_atts MAYQ   contenidocuerpo;

etiquetas_encabezado ::= etiquetas_encabezado MENQ et_encabezado:x  {: last.add(x); :}
                        |MENQ et_encabezado:x     {: last.add(x); :};

et_encabezado ::= etiquetacss:r    {: RESULT = r; :}
                 |etiquetacjs:r    {: RESULT = r; :}
                 |etiquetatitulo:r {: RESULT = r; :};

etiquetacss ::=  ICJS list_atts MAYQ  MENQ FCJS MAYQ    {: RESULT = parser.crearNodo("cjs","",0,0); :};
etiquetacjs ::=  ICSS list_atts MAYQ  MENQ FCSS MAYQ    {: RESULT = parser.crearNodo("ccss","",0,0); :};
etiquetatitulo::= ITITULO MAYQ  MENQ FTITULO MAYQ       {: RESULT = parser.crearNodo("titulo","",0,0); :};


fin_encabezado ::= MENQ FENCABEZADO MAYQ    MENQ FHTML MAYQ
                  |MENQ FENCABEZADO MAYQ    MENQ ICUERPO:x list_atts MAYQ   {: parser.m1("cuerpo","",x.getLinea(),x.getColumna()); :} contenidocuerpo;


contenidocss ::= FCSS MAYQ MENQ FENCABEZADO MAYQ    MENQ FHTML MAYQ
                 FCSS MAYQ MENQ FENCABEZADO MAYQ    MENQ ICUERPO:x  list_atts MAYQ   {: parser.m1("cuerpo","",x.getLinea(),x.getColumna()); :} contenidocuerpo;
   
contenidocuerpo ::= etiquetas_cuerpo MENQ FCUERPO MAYQ MENQ FHTML MAYQ
                    |MENQ FCUERPO MAYQ MENQ FHTML MAYQ;

etiquetas_cuerpo ::= etiquetas_cuerpo MENQ et_cuerpo
                     |MENQ et_cuerpo;


et_cuerpo ::=  IPANEL:x   list_atts    MAYQ  {:parser.m2("panel","",x.getLinea(),x.getColumna()); :}        contenidopanel     
              |ITEXTO:x   list_atts    MAYQ  {:parser.m2("texto","",x.getLinea(),x.getColumna());:}         MENQ FTEXTO      MAYQ
              |IIMAGEN:x  list_atts    MAYQ  {:parser.m2("imagen","",x.getLinea(),x.getColumna());:}        MENQ FIMAGEN     MAYQ
              |IBOTON:x   list_atts    MAYQ  {:parser.m2("boton","",x.getLinea(),x.getColumna());:}         MENQ FBOTON      MAYQ
              |IENLACE:x  list_atts    MAYQ  {:parser.m2("enlace","",x.getLinea(),x.getColumna());:}        MENQ FENLACE     MAYQ
              |ITABLA:y   list_atts    MAYQ  contenidotabla:x {: x.linea=y.getLinea(); x.columna=y.getColumna(); parser.Tadd(x); :}
              |ITEXTO_A:x list_atts    MAYQ  {:parser.m2("texto_a","",x.getLinea(),x.getColumna());:}       MENQ FTEXTO_A    MAYQ
              |ICAJA_TEXTO:x list_atts MAYQ  {:parser.m2("caja_texto","",x.getLinea(),x.getColumna());:}    MENQ FCAJA_TEXTO MAYQ
              |ICAJA:x    list_atts    MAYQ  {:parser.m2("caja","",x.getLinea(),x.getColumna());:}          MENQ FCAJA       MAYQ  
              |ISPINNER:x list_atts    MAYQ  {:parser.m2("spinner","",x.getLinea(),x.getColumna());:}       MENQ FSPINNER    MAYQ
              |ISALTOF:x               MAYQ  {:parser.m2("salto","",x.getLinea(),x.getColumna());:};

contenidotabla ::= etiquetastabla:x     MENQ     FTABLA      MAYQ  {: RESULT = x; :}
                   |MENQ     FTABLA      MAYQ                      {: RESULT = parser.crearNodo("tabla","",0,0); :};
                    
etiquetastabla ::= etiquetastabla:x MENQ et_fila:y {: x.add(y); RESULT = x;  :}
                  |MENQ et_fila:x  {:  RESULT = parser.crearNodo("tabla","",0,0); RESULT.add(x); :};

et_fila        ::= IFIL_T:y list_atts MAYQ  contenidofila:x {: x.linea=y.getLinea(); x.columna=y.getColumna();  RESULT=x; :};

contenidofila   ::= etiquetasfila:x MENQ FFIL_T MAYQ {: RESULT=x; :}
                    |MENQ FFIL_T MAYQ                {: RESULT = parser.crearNodo("fil_t","",0,0); :};

etiquetasfila  ::= etiquetasfila:x MENQ et_cb_ct:y {: x.add(y); RESULT=x; :}
                   |MENQ et_cb_ct:x                {: RESULT = parser.crearNodo("fil_t","",0,0); RESULT.add(x); :};

et_cb_ct       ::= ICB:y list_atts MAYQ contenidocb:x  {:x.linea=y.getLinea(); x.columna=y.getColumna();    RESULT = x; :} 
                  |ICT:y list_atts MAYQ contenidoct:x  {:x.linea=y.getLinea(); x.columna=y.getColumna();    RESULT = x; :};


contenidocb    ::= contenidos_celdas:x MENQ FCB MAYQ   {:  RESULT = x; :}
                  |MENQ FCB MAYQ                       {:RESULT = parser.crearNodo("cb","",0,0);:};

contenidoct    ::= contenidos_celdas:x MENQ FCT MAYQ   {:  RESULT = x; :}
                  |MENQ FCT MAYQ                       {:RESULT = parser.crearNodo("ct","",0,0);:};


    

contenidopanel ::= etiquetas_cuerpo    MENQ     FPANEL      MAYQ {: parser.pop(); :}
                  |MENQ     FPANEL      MAYQ  {: parser.pop(); :};  


list_atts ::= {:    RESULT = parser.crearNodo("propiedades","",0,0);   :}
             |list_atts2:x {:   RESULT = x;  :};

list_atts2 ::= list_atts2:x atributo:y PYC {:
                                x.add(y);
                                RESULT=x;    
                            :}
              |atributo:x PYC {: 
                                RESULT = parser.crearNodo("propiedades","",0,0); 
                                RESULT.add(x);
                            :};
        
atributo ::= att_ruta:r     {: RESULT = r;   :}
            |att_click:r    {: RESULT = r;   :}
            |att_valor:r    {: RESULT = r;   :}
            |att_id:r       {: RESULT = r;   :}
            |att_grupo:r    {: RESULT = r;   :}
            |att_alto:r     {: RESULT = r;   :}
            |att_ancho:r    {: RESULT = r;   :}
            |att_alineado:r {: RESULT = r;   :};

att_ruta  ::= RUTA:x IGUAL STRING_LITERAL:y            {: RESULT = parser.crearNodo("ruta",y.getCadena(),x.getLinea(),x.getColumna()); :}; 

att_click ::= CLICK:x IGUAL STRING_LITERAL:y APAR CPAR {: RESULT = parser.crearNodo("click",y.getCadena(),x.getLinea(),x.getColumna()); :};

att_valor ::= VALOR:x IGUAL STRING_LITERAL:y           {: RESULT = parser.crearNodo("valor",y.getCadena(),x.getLinea(),x.getColumna()); :};

att_id    ::= ID:x IGUAL STRING_LITERAL:y              {: RESULT = parser.crearNodo("id",y.getCadena(),x.getLinea(),x.getColumna()); :};

att_grupo ::= GRUPO:x IGUAL STRING_LITERAL:y           {: RESULT = parser.crearNodo("grupo",y.getCadena(),x.getLinea(),x.getColumna()); :};

att_alto  ::= ALTO:x IGUAL STRING_LITERAL:y            {: RESULT = parser.crearNodo("alto",y.getCadena(),x.getLinea(),x.getColumna()); :};

att_ancho ::= ANCHO:x IGUAL STRING_LITERAL:y           {: RESULT = parser.crearNodo("ancho",y.getCadena(),x.getLinea(),x.getColumna()); :};

att_alineado  ::= ALINEADO:x IGUAL STRING_LITERAL:y    {: RESULT = parser.crearNodo("alineado",y.getCadena(),x.getLinea(),x.getColumna()); :};


contenidos_celdas ::= contenidos_celdas:x cont_celda:y {: x.add(y);  RESULT = x ; :}
                    |cont_celda:x {: RESULT = parser.crearNodo("ct","",x.linea,x.columna);  RESULT.add(x);  :};

cont_celda        ::= I_D
                     |IBOTON:x   list_atts    MAYQ        MENQ FBOTON      MAYQ {: RESULT = parser.crearNodo("boton","",x.getLinea(),x.getColumna());  :}
                     |IIMAGEN:x  list_atts    MAYQ        MENQ FIMAGEN     MAYQ {: RESULT = parser.crearNodo("imagen","",x.getLinea(),x.getColumna()); :};



